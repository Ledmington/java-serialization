plugins {
	id 'java-library'
	id 'com.diffplug.spotless' version '6.19.0'
	id 'com.github.ben-manes.versions' version '0.46.0'
	id 'com.adarshr.test-logger' version '3.2.0'
}

repositories {
	mavenCentral()
}

static def path(String... args) {
	return String.join(File.separator, args)
}

String appName = 'java-serialization'
String version = '0.1.0'
String appDescription = 'A simple, efficient and fast serialization library.'
String author = 'Filippo Barbari'
String authorMail = 'filippo.barbari@gmail.com'

java {
	sourceCompatibility = '17'
	targetCompatibility = '17'
}

ext.junitVersion = "5.9.2"
dependencies {
	testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
	testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

test {
	useJUnitPlatform()
	// failFast true
}

testlogger {
	theme 'standard'

	showExceptions true
	showStackTraces true
	showFullStackTraces true
	showCauses true

	slowThreshold 2000

	showSummary true
	showSimpleNames false
	showPassed true
	showSkipped true
	showFailed true
	showOnlySlow false

	showStandardStreams true
	showPassedStandardStreams false
	showSkippedStandardStreams false
	showFailedStandardStreams true
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs << '-Xdiags:verbose'
	options.compilerArgs << '-Xlint:all'
	options.compilerArgs << '-Werror'
	options.deprecation = true
	options.encoding = 'UTF-8'
}

spotless {
	format 'misc', {
		target '*.gradle', '*.md', '.gitignore'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
		setEncoding('utf-8')
	}

	java {
		target fileTree('.') {
			include path('**', '*.java')
			exclude path('**', 'build', '**'), path('**', 'build-*', '**'), path('**', 'bin', '**')
		}
		removeUnusedImports()
		formatAnnotations()
		trimTrailingWhitespace()
		endWithNewline()
		setEncoding('utf-8')
		palantirJavaFormat('2.26.0')

		// check https://javadoc.io/doc/com.diffplug.spotless/spotless-plugin-gradle/latest/com/diffplug/gradle/spotless/JavaExtension.html
		importOrder('java', 'javax', 'javafx', 'com', 'org', group as String)

		licenseHeader String.join("\n",
			"/*",
			"* ${appName} - ${appDescription}",
			"* Copyright (C) 2023-${new Date().format('yyyy')} ${author} <${authorMail}>",
			"*",
			"* This program is free software: you can redistribute it and/or modify",
			"* it under the terms of the GNU General Public License as published by",
			"* the Free Software Foundation, either version 3 of the License, or",
			"* (at your option) any later version.",
			"*",
			"* This program is distributed in the hope that it will be useful,",
			"* but WITHOUT ANY WARRANTY; without even the implied warranty of",
			"* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the",
			"* GNU General Public License for more details.",
			"*",
			"* You should have received a copy of the GNU General Public License",
			"* along with this program.  If not, see <http://www.gnu.org/licenses/>.",
			"*/",
		)
	}
}

tasks.compileJava.dependsOn(spotlessApply)
